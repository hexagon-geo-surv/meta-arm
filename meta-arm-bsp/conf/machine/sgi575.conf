# Configuration for Arm SGI575 development board

#@TYPE: Machine
#@NAME: SGI575
#@DESCRIPTION: Machine configuration for SGI575

require conf/machine/include/arm/armv8-2a/tune-cortexa75.inc

EXTRA_IMAGEDEPENDS += "virtual/control-processor-firmware"

EXTRA_IMAGEDEPENDS += "trusted-firmware-a"

KERNEL_IMAGETYPE ?= "Image"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
SERIAL_CONSOLES = "115200;ttyAMA0"

#grub-efi
EFI_PROVIDER ?= "grub-efi"
MACHINE_FEATURES += "efi"

IMAGE_FSTYPES += "cpio.gz wic"

WKS_FILE ?= "sgi575-efidisk.wks"
WKS_FILE_DEPENDS:append = " ${EXTRA_IMAGEDEPENDS}"


# As this is a virtual target that will not be used in the real world there is
# no need for real SSH keys.
MACHINE_EXTRA_RRECOMMENDS += "ssh-pregen-hostkeys"

TEST_TARGET = "OEFVPTarget"
TEST_TARGET_IP = "127.0.0.1:8022"

# FVP Config
FVP_ARCH_DIR = "Linux64_GCC-6.4"
FVP_PROVIDER ?= "fvp-sgi575"
FVP_EXE ?= "FVP_CSS_SGI-575"

# FVP Parameters
FVP_CONFIG[css.scp.ROMloader.fname] ?= "${DEPLOY_DIR_IMAGE}/scp_romfw.bin"
FVP_CONFIG[css.mcp.ROMloader.fname] ?= "${DEPLOY_DIR_IMAGE}/mcp_romfw.bin"
FVP_CONFIG[board.flashloader0.fname] ?= "${DEPLOY_DIR_IMAGE}/fip.bin"

FVP_CONFIG[board.hostbridge.userNetworking] ?= "true"
FVP_CONFIG[board.hostbridge.userNetPorts] ?= "8022=22"

FVP_CONSOLE = "terminal_s1"
FVP_TERMINALS[soc.terminal_s0] ?= "Secure Console"
FVP_TERMINALS[soc.terminal_s1] ?= "Console"

# Boot image
FVP_DATA ?= "css.scp.armcortexm7ct=${DEPLOY_DIR_IMAGE}/scp_ramfw.bin@0x0BD80000"
