# Configuration for Arm N1SDP development board

#@TYPE: Machine
#@NAME: N1SDP machine
#@DESCRIPTION: Machine configuration for N1SDP

require conf/machine/include/arm/armv8-2a/tune-neoversen1.inc

KERNEL_IMAGETYPE = "Image"

IMAGE_FSTYPES += "wic wic.gz wic.bmap tar.bz2 ext4"

SERIAL_CONSOLES = "115200;ttyAMA0"

# Set default WKS
WKS_FILE ?= "n1sdp-efidisk.wks"
IMAGE_EFI_BOOT_FILES ?= "n1sdp-multi-chip.dtb n1sdp-single-chip.dtb"
WKS_FILE_DEPENDS:append = " ${EXTRA_IMAGEDEPENDS}"

# Use kernel provided by yocto
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
PREFERRED_VERSION_linux-yocto ?= "5.19%"

# RTL8168E Gigabit Ethernet Controller is attached to the PCIe interface
MACHINE_ESSENTIAL_EXTRA_RDEPENDS += "linux-firmware-rtl8168"

EXTRA_IMAGEDEPENDS += "trusted-firmware-a"
EXTRA_IMAGEDEPENDS += "virtual/control-processor-firmware"

#UEFI EDK2 firmware
EXTRA_IMAGEDEPENDS += "edk2-firmware"

#optee
PREFERRED_VERSION_optee-os ?= "3.18.%"

#grub-efi
EFI_PROVIDER ?= "grub-efi"
MACHINE_FEATURES += "efi"

# SD-Card firmware
EXTRA_IMAGEDEPENDS += "sdcard-image-n1sdp"


# As this is a virtual target that will not be used in the real world there is
# no need for real SSH keys.
MACHINE_EXTRA_RRECOMMENDS += "ssh-pregen-hostkeys"

TEST_TARGET = "OEFVPTarget"
TEST_TARGET_IP = "127.0.0.1:8022"

# FVP Config
FVP_PROVIDER ?= "fvp-neoverse-v1-native"
FVP_EXE ?= "FVP_RD_V1"

# FVP Parameters
FVP_DATA ?= "css.scp.armcortexm7ct=${DEPLOY_DIR_IMAGE}/scp_ramfw.bin@0x0BD80000"
FVP_CONFIG[css.scp.ROMloader.fname] ?= "${DEPLOY_DIR_IMAGE}/scp_romfw.bin"
FVP_CONFIG[css.mcp.ROMloader.fname] ?= "${DEPLOY_DIR_IMAGE}/mcp_romfw.bin"
FVP_CONFIG[css.trustedBootROMloader.fname] ?= "${DEPLOY_DIR_IMAGE}/bl1-n1sdp.bin"
FVP_CONFIG[board.flashloader0.fname] ?= "${DEPLOY_DIR_IMAGE}/fip-n1sdp.bin"

FVP_CONFIG[board.virtio_net.enabled] ?= "1"
FVP_CONFIG[board.virtio_net.hostbridge.userNetworking] ?= "1"
FVP_CONFIG[board.virtio_net.hostbridge.userNetPorts] = "8022=22"

FVP_CONSOLE = "terminal_s0"
FVP_TERMINALS[soc.terminal_s0] ?= "Secure Console"
FVP_TERMINALS[soc.terminal_s1] ?= "Console"

# Boot image
FVP_CONFIG[board.virtioblockdevice.image_path] ?= "${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.wic"
