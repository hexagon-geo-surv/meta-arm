# Configuration for Arm Neoverse V2 development board

#@TYPE: Machine
#@NAME: rd-v2
#@DESCRIPTION: Machine configuration for Neoverse V2

require conf/machine/include/arm/arch-armv9a.inc

EXTRA_IMAGEDEPENDS += "virtual/control-processor-firmware"
EXTRA_IMAGEDEPENDS += "trusted-firmware-a"

KERNEL_IMAGETYPE ?= "Image"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-yocto"
SERIAL_CONSOLES = "115200;ttyAMA0"

#grub-efi
EFI_PROVIDER ?= "grub-efi"
MACHINE_FEATURES += "efi"

IMAGE_FSTYPES += "cpio.gz wic"

WKS_FILE ?= "efi-disk.wks.in"
EFI_PROVIDER ?= "${@bb.utils.contains("DISTRO_FEATURES", "systemd", "systemd-boot", "grub-efi", d)}"
WKS_FILE_DEPENDS:append = " ${EXTRA_IMAGEDEPENDS}"


#FVP Config
TEST_TARGET = "OEFVPTarget"
TEST_SUITES = "fvp_boot"

# FVP Config
FVP_PROVIDER ?= "fvp-rd-v2-native"
FVP_EXE ?= "FVP_RD_V2"

# FVP Parameters
FVP_DATA ?= "css.scp.armcortexm7ct=scp_ramfw.bin@0x0BD80000"
FVP_CONFIG[css.mcp.ROMloader.fname] ?= "mcp_romfw.bin"
FVP_CONFIG[css.scp.ROMloader.fname] ?= "scp_romfw.bin"
FVP_CONFIG[css.trustedBootROMloader.fname] ?= "bl1-rdn2.bin"
FVP_CONFIG[board.flashloader0.fname] ?= "fip-rdn2.bin"

FVP_CONFIG[board.virtio_net.enabled] ?= "1"
FVP_CONFIG[board.virtio_net.hostbridge.userNetworking] ?= "1"
# Tell testimage to connect to localhost:2222, and forward that to SSH in the FVP.
FVP_CONFIG[board.virtio_net.hostbridge.userNetPorts] = "2222=22"

FVP_CONSOLE = "terminal_s0"
FVP_TERMINALS[soc.terminal_s0] ?= "Secure Console"
FVP_TERMINALS[soc.terminal_s1] ?= "Console"
#FVP_TERMINALS[soc.terminal_mcp] ?= "MCP Console"

# Boot image
FVP_CONFIG[board.virtioblockdevice.image_path] ?= "${IMAGE_NAME}.wic"

FVP_CONFIG[css.cmn_kampos.force_rnsam_internal] = "true"
FVP_CONFIG[css.gic_distributor.ITS-device-bits] = "20"
